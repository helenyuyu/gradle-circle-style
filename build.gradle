plugins {
  id 'java'
  id 'eclipse'
  id 'idea'
  id 'com.gradle.plugin-publish' version '0.9.7'
  id 'org.inferred.processors' version '1.2.15'
}

repositories {
    jcenter()
    mavenCentral()
}

sourceCompatibility = 1.8

dependencies {
  compile gradleApi()

  processor 'org.inferred:freebuilder:1.14.4'

  testCompile gradleTestKit()
  testCompile 'com.github.stefanbirkner:system-rules:1.16.0'
  testCompile 'com.google.guava:guava:23.0-android'
  testCompile 'junit:junit:4.12'
  testCompile 'org.assertj:assertj-core:2.8.0'
  testCompile 'org.mockito:mockito-core:2.9.0'
  testCompile 'me.nallar.whocalled:WhoCalled:1.1'
}

// Extra validation steps
tasks.check.dependsOn tasks.javadoc

group = 'com.palantir'
if (System.env.CIRCLE_TAG) {
  version = System.env.CIRCLE_TAG.replaceAll('^v', '')
}

pluginBundle {
  website = 'https://github.com/palantir/gradle-circle-style'
  vcsUrl = 'https://github.com/palantir/gradle-circle-style'

  plugins {
    circleStylePlugin {
      displayName = 'Circle style plugin'
      id = 'com.palantir.circle.style'
      description = 'Integrating Gradle\'s checkstyle and findbugs output into Circle'
      tags = ['java', 'circle', 'checkstyle', 'findbugs']
    }
  }
}

String envOrThrow(String key) {
  def value = System.getenv(key)
  if (!value) {
    throw new RuntimeException("\$$key not set")
  }
  return value
}

// Read publication secrets from environment
task setupPublishProperties {
  tasks.publishPlugins.dependsOn it
  doLast {
    System.properties.setProperty('gradle.publish.key', envOrThrow('gradlePublishKey'))
    System.properties.setProperty('gradle.publish.secret', envOrThrow('gradlePublishSecret'))
  }
}
